plugins {
    id 'java'

    id 'me.bristermitten.pdm' version '0.0.30'
    id 'com.github.johnrengelman.shadow' version '5.0.0'

    id 'net.minecrell.plugin-yml.bukkit' version '0.3.0'
}

group 'com.nextplugins'
version '1.1.0'

tasks.build.dependsOn('pdm', 'shadowJar')

repositories {
    mavenCentral()

    // spigot

    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://oss.sonatype.org/content/groups/public/' }

    // plugins

    maven { url = 'https://repo.codemc.org/repository/maven-public' }
    maven { url = 'https://repo.citizensnpcs.co/' }

    maven { url = 'https://jitpack.io/' }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.12.2-R0.1-SNAPSHOT'

    compileOnly 'com.github.PlaceholderAPI:PlaceholderAPI:2.10.9'
    compileOnly 'com.gmail.filoghost.holographicdisplays:holographicdisplays-api:2.4.0'
    compileOnly 'net.citizensnpcs:citizens:2.0.27-SNAPSHOT'

    implementation 'com.github.HenryFabio:configuration-injector:main-SNAPSHOT'
    implementation 'org.bstats:bstats-bukkit:1.8'

    pdm 'com.github.NextPlugins:data-converter:experimental-0.0.1'

    pdm 'com.github.SaiintBrisson.command-framework:bukkit:1.2.0'
    pdm 'com.github.SaiintBrisson.command-framework:shared:1.2.0'

    pdm 'com.github.HenryFabio:sql-provider:9561f20fd2'
    pdm 'com.github.HenryFabio:inventory-api:2.0.3'

    pdm 'de.tr7zw:item-nbt-api:2.7.1'

    def lombok = 'org.projectlombok:lombok:1.18.16'
    compileOnly lombok
    annotationProcessor lombok
}

bukkit {
    main = 'com.nextplugins.cash.NextCash'
    authors = ['Henry_Fabio', 'Eike', 'Gabriel Santos', 'Yuhtin']
    website = 'https://github.com/NextPlugins'
    version = "${project.version}"
    softDepend = ['Citizens', 'HolographicDisplays', 'PlaceholderAPI']
}

shadowJar {
    archiveName("${project.name}.jar")
    relocate 'com.henryfabio.minecraft.configinjector', 'com.nextplugins.cash.libs.configinjector'
    relocate 'org.bstats', 'com.nextplugins.cash.libs.bstats'
}

compileJava {
    options.encoding('UTF-8')

    sourceCompatibility('1.8')
    targetCompatibility('1.8')
}
